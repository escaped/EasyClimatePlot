set term FORMAT
set title "TITLE"
set output "OUTPUTPATH/NAME.EXTENSION"

## allgemeines
set style fill solid #border -1
set border 11
set boxwidth 0.5
set object 1 rectangle from graph 0,0 to graph 1,1 behind fc rgbcolor "#EEEEff" lw 0

###############
# Plot
###############

set multiplot
#######################
##
## Oberhalb von 100 mm
##
#######################

set xrange [ 0 : 12.5 ]
set y2range [ 100 : 300 ]
unset xtics
set y2tics 100,100,300

# wir wollen die ytics hier garnicht.. daher müssen wir eben ein wenig hacken.
set ytics 100,1000,200 tc rgbcolor "#ffffff" 

set size 1,0.3
set origin 0,0.616
set y2label " "
set ylabel " "
set grid y2tics
unset key

plot "FILE" using 1:6 with boxes axes x1y2 lc rgbcolor "#bbbbff" title "mittl. Niederschlag"
unset grid

#######################
##
## Unterhalb von 100 mm
##
#######################
unset title
set key below

set size 1,0.7

set ytics 0,10, 60 tc rgbcolor "#000000"
set grid ytics
set format "  %g"
set origin 0,0
set yrange [0:60]
set xtics nomirror

set y2tics 0,20,100 tc rgbcolor "#000000"
set y2range [ 0 : 99 ]
set y2tics nomirror

set xlabel "Monat"
set xmtics
set ylabel "Temperatur (°C)" #tc rgbcolor "#FF0000" font "Arial,18"
set y2label "Niederschlag (mm)" #tc rgbcolor "#006600"

plot "FILE" using 1:6 with boxes axes x1y2 lc rgbcolor "#bbbbff" title "mittl. Niederschlag",\
     "FILE" using 1:2:xtic(1) with lines lc -1 lw 1.5 title "mittl. Temperatur" ,\
     "FILE" using 1:3 with lines lc 1 lw 1.5 title "mittl. max. Temperatur",\
     "FILE" using 1:4 with lines lc 3 lw 1.5 title "mittl. min. Temperatur"
set nomultiplot
